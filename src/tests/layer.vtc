varnishtest "Layering test"

feature cmd "getent hosts localhost"

server s1 {
       rxreq
       txresp
} -start

varnish v1 -vcl+backend {
	import ${vmod_dynamic};
	import directors;
	import std;

	sub vcl_init {
		new d1 = dynamic.director(port = "${s1_port}");
		new rr = directors.round_robin();
		rr.add_backend(d1.backend("localhost"));
		std.log("Resolve: " + rr.backend().resolve());
	}

	sub vcl_recv {
		std.log("Resolve: " + rr.backend().resolve());
		set req.backend_hint = rr.backend();
	}

	sub vcl_backend_error {
		# the director may resolve ::1 first
		return (retry);
	}
} -start

varnish v1 -cliok "backend.list"

logexpect l1 -v v1 -d 1 -g raw {
	expect *    0 VCL_Log "Resolve: d1"
	expect * 1001 VCL_Log "Resolve: d1"
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 200
} -run

logexpect l1 -wait
varnish v1 -expect VBE.vcl1.d1(${s1_addr}:${s1_port}).req == 1
varnish v1 -expect LCK.dynamic.director.creat > 0
varnish v1 -expect LCK.dynamic.backend.creat > 0
